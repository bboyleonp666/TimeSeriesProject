/*------------------------------------------------------------------------------------------------------------------*/
/*Locate Dataset Directory*/
%macro datasets_dir;
%local fr rc cwd;
%let rc = %sysfunc(filename(fr,.));
%let cwd = %sysfunc(pathname(&fr));
%let rc = %sysfunc(filename(fr));

%let data = data;
%let datasets_dir = &cwd\data;
&datasets_dir
%mend datasets_dir;
/*------------------------------------------------------------------------------------------------------------------*/
/*Locate Dataset Directory*/
%macro cwd;
%local fr rc cwd;
%let rc = %sysfunc(filename(fr,.));
%let cwd = %sysfunc(pathname(&fr));
%let rc = %sysfunc(filename(fr));
&cwd
%mend cwd;
/*------------------------------------------------------------------------------------------------------------------*/
/*File Variable*/
%let NetIncome_file = Flight_Comany_NetIncome_2005_2019.csv;
%let Income_file = Flight_Comany_Income_2005_2019.csv;
%let Oil_file = International_Price_of_Crude_Oil_2005_2019.csv;
/*Data for Net Income*/
data temp;
infile "%datasets_dir\&NetIncome_file" firstobs = 2 delimiter = ",";
input Company ShortName $ time $ NetIncome;
proc sort data = temp;
by time;
proc transpose data = temp out = Flight_NetIncome_Wide prefix = NetIncome;
by time;
id company;
var netincome;
data Flight_NetIncome;
set flight_netincome_wide(drop = _name_);
rename
	Netincome2610 = net_China
	Netincome2612 = net_AirChina
	Netincome2618 = net_Eva;
label
	time = "~る"
	Netincome2610 = "地瑜颛Q (China)"
	Netincome2612 = "い瑜颛Q (Air China)"
	Netincome2618 = "aをQ (EVA)";
if time < 200803 then delete;
/*proc print data = flight_netincome label; run;*/
/*Data for Income*/
data temp;
infile "%datasets_dir\&Income_file" firstobs = 2 delimiter = ",";
input Company ShortName $ time $ Income;
proc sort data = temp;
by time;
proc transpose data = temp out = Flight_Income_Wide prefix = Income;
by time;
id company;
var income;
data Flight_Income;
set flight_income_wide(drop = _name_);
rename
	income2610 = income_China
	income2612 = income_AirChina
	income2618 = income_Eva;
label
	time = "~る"
	income2610 = "地枥绂 (China)"
	income2612 = "い枥绂 (Air China)"
	income2618 = "a犁Μ (EVA)";
if time < 200803 then delete;
/*proc print data = flight_income label; run;*/
/*Data for Crude Oil*/
proc import datafile = "%datasets_dir\&Oil_file" out = temp 
	dbms = csv replace;
data Oil_Price;
set temp;
rename POILBREUSDM = Oil_Price;
label POILBREUSDM = "o基 (Oil Price)";
	/*Process Time Data*/
	Year = year(date);
	if month(date) = 1 then YEAR = year - 1;
	if month(date) = 1 then MONTH = "12";
	else MONTH = cat("0", month(date) - 1);
	Time = cat(YEAR, MONTH);

/*Keep year after 2008*/
if time < 200803 then delete;
/*Keep only two columns*/
keep POILBREUSDM Time;
proc sort;
by time;
run;
/*proc print label; run;*/

/*Merge Income and Net Income*/
data Flight;
merge Flight_Income Flight_NetIncome Oil_Price;
by time;
drop income_airchina net_airchina; *drop data related to Air China;
run;
/*proc print; run;*/

data Flight Test;
set flight;
if time <= "201812" then output Flight;
else output Test;
run;
/*------------------------------------------------------------------------------------------------------------------*/
/*Model Selection for VAR*/
%macro VARModel(dataset, var, P, DIF);
ods select none;
proc varmax data = &dataset;
model &var / p = &P dify = (&DIF);
ods output LogLikelihood = LLH;
ods output InfoCriteria = IC;
/*output out=for lead=5;*/
run;
data Info;
set LLH IC;
if _n_ in (1 4);
drop cvalue1;
if label1 = "癸计阀" then label1 = "LogLike";
rename 
	label1 = Criterion
	nvalue1 = Value;
proc transpose data = Info 
	out = Info_T(drop = _name_);
var value;
id Criterion;
data InfoTable_&P;
p = &P;
set info_t;
run;
ods select all;
%mend VARModel;
/*------------------------------------------------------------------------------------------------------------------*/
%macro VARModels(dataset, var, DIF);
%do i = 1 %to 5;
	%VARModel(&dataset, &var, &i, &DIF);
%end;
data AllModel;
set InfoTable_:;
proc print data = AllModel; run;
%mend VARModels;
/*------------------------------------------------------------------------------------------------------------------*/
/*Output as pdf file*/
ods pdf file = "out.pdf";
/*------------------------------------------------------------------------------------------------------------------*/
/* read data
	200803 - 201712 -- Flight(Train)
	201803 - 201909 -- Test*/
title "Flight Training Data Table";
proc print data = flight; run;
title "Flight Testing Data Table";
proc print data = test; run;

/*Standardize the data*/
proc standard data = flight mean = 0 std = 1 out = Flight_Std;
var income: net: oil_price;
/*proc print data = flight_Std; run;*/
/*------------------------------------------------------------------------------------------------------------------*/
/*Time Series Plots*/
/*Income*/
ods graphics on / width = 1080px;
proc sgplot data = flight_std;
title height = 25pt "Time Series Plot for Income and Oil Price";
series x = time y = income_china / lineattrs = (color=blue thickness=3);
series x = time y = income_eva / lineattrs = (color=green thickness=3);
series x = time y =  oil_price / lineattrs = (color=red thickness=3);
yaxis label = "Normalized Data" labelattrs = (size=15) valueattrs = (size=10);
xaxis label = "Season" labelattrs = (size=15);
/*xaxis label = "Season" labelattrs = (size=15)  valuesrotate = diagnoal2;*/
run;
/*Net Income*/
proc sgplot data = flight_std;
title height = 25pt "Time Series Plot for Net Income and Oil Price";
series x = time y = net_china / lineattrs = (color=blue thickness=3);
series x = time y = net_eva / lineattrs = (color=green thickness=3);
series x = time y =  oil_price / lineattrs = (color=red thickness=3);
yaxis label = "Normalized Data" labelattrs = (size=15) valueattrs = (size=10);
xaxis label = "Season" labelattrs = (size=15);
run;
/*------------------------------------------------------------------------------------------------------------------*/
/*Dataset Adjustment*/
data flight_adj;
set flight;
/*Add min value to variables containing negative value
   Add 1 to avoid facing negative infinite problem when applying log transformation*/
net_China = net_China + 1098628 + 1;
net_Eva = net_Eva + 1652777 + 1;

/*log transformation*/
LogChina = log(income_china);
LogEva = log(income_eva);
net_LogChina = log(net_China);
net_LogEva = log(net_Eva);
LogOil = log(oil_price);

label
	LogChina = "地枥绂 (China)"
	LogEva = "a犁Μ (EVA)"
	net_LogChina = "地瑜颛Q (China)"
	net_LogEva = "aをQ (EVA)";
Term = _n_;
title "Adjust Flight Data Table";
proc print; run;

proc sgplot data = flight_adj;
title height = 25pt "Time Series Plot for Income and Oil Price";
series x = time y = logchina/ lineattrs = (color=blue thickness=3);
series x = time y = logeva / lineattrs = (color=green thickness=3);
/*series x = time y =  logoil / lineattrs = (color=red thickness=3);*/
yaxis label = "Log Transformed Data" labelattrs = (size=15) valueattrs = (size=10);
xaxis label = "Season" labelattrs = (size=15);
run;
/*Net Income*/
proc sgplot data = flight_adj;
title height = 25pt "Time Series Plot for Net Income and Oil Price";
series x = time y = net_logchina / lineattrs = (color=blue thickness=3);
series x = time y = net_logeva / lineattrs = (color=green thickness=3);
/*series x = time y =  oil_price / lineattrs = (color=red thickness=3);*/
yaxis label = "Log Transformed Data" labelattrs = (size=15) valueattrs = (size=10);
xaxis label = "Season" labelattrs = (size=15);
run;

title "Check Breakpoint of Log Income of China";
proc autoreg data = flight_adj;
model logchina = term / chow = (5 6 7 8);
model net_LogChina = term / chow = (2 3 4);
ods select ChowTest DiagnosticsPanel;
run;
data flight_adj;
set flight_adj;
if Term > 7;
run;
/*We hope to keep the long term relation, so we kept the data after Term = 7*/

%let dataset = flight_adj;                         *Name of dataset;
%let log_income = Log:;                         *Variables for all the log transformated income;
%let log_net = net_Log: LogOil;             *Variables for all the log transformated net income;
/*------------------------------------------------------------------------------------------------------------------*/
/*Fit VAR model of income*/
/*Analysis for Income*/
/*Dickey-Fuller Test
   H0: non-stationary
   Some Reject H0 while some not*/
title "Dickey-Fuller Unit Root Test for Log Income";
proc varmax data = flight_adj;
model &log_income / p = 1 dftest;
ods select DFTest;
run;
/*first-differentiated*/
title "Dickey-Fuller Unit Root Test for Diff Log Income";
proc varmax data = flight_adj;
model &log_income / p = 1 dify = (1) dftest;
ods select DFTest;
run;
/*All are Stationary!!!*/

/*Model selection for Operating Income*/
title "Model Criterion for Log Income";
%VARModels(&dataset, &log_income, 1);
/*Pick the one with smallest AIC value: 2*/
title "Model for Log Income with p = 2";
proc varmax data = flight_adj;
model &log_income / p = 2 noint lagmax = 3 dify = (1)
									dftest cointtest=(johansen)
									print = (estimates diagnose);
run;
/*Granger Causality Test*/
title "Granger Causality Test";
proc varmax data = flight_adj;
model logchina logeva = LogOil / p = 2 difx = (1) dify = (1);
causal group1 = (logchina logeva) group2 = (LogOil);
causal group1 = (logchina) group2 = (logeva);
ods select CausalityTest GroupVars;
run;
/*
proc varmax data = flight_adj plot = impulse;
model logchina logeva = LogOil  / p = 2 difx = (1) dify = (1)
													  printform = univariate
													  print = (impulsx=(all) estimates);
run;
*/
/*------------------------------------------------------------------------------------------------------------------*/
/*Analysis for Net Income*/
title "Dickey-Fuller Unit Root Test for Log Net Income";
proc varmax data = flight_adj;
model &log_net / p = 1 dftest;
ods select DFTest;
run;
/*Inspect Trend P-value
   Some are non-stationary some are stationary*/
title "Dickey-Fuller Unit Root Test for Diff Log Net Income";
proc varmax data = flight_adj;
model &log_net / p = 1 dify = (1) dftest;
ods select DFTest;
run;
/*All are Stationary!!!*/

/*Model selection for Net income*/
title "Model Criterion for Log Net Income";
%VARModels(&dataset, &log_net, 1);
/*Min AIC: p = 2*/
title "Model for Log Net Income with p = 2";
proc varmax data = flight_adj;
model &log_net / p = 2 noint lagmax = 3 dify = (1)
							dftest cointtest=(johansen)
							print = (estimates diagnose);
run;
title "Granger Causality Test";
proc varmax data = flight_adj;
model net_log: =  LogOil / p = 2 dify = (1) difx = (1);
causal group1 = (net_log:) group2 = (LogOil);
causal group1 = (net_logchina) group2 = (net_logeva);
ods select CausalityTest GroupVars;
run;
/*
proc varmax data = flight_adj plot = impulse;
model net_log: = LogOil  / p = 2 dify = (1) difx = (1) 
										  printform = univariate
										  print = (impulsx=(all) estimates);
run;
*/

/*Fit ARIMA model*/
/*------------------------------------------------------------------------------------------------------------------*/
/*い地瑾 China*/
title "Fit Model for Log Income of China";
proc arima data = Flight_adj;
identify var = logchina nlag = 15 stationarity = (adf=4);
estimate p = 5 method = ml;
ods select ChiSqAuto StationarityTests SeriesCorrPanel ChiSqAuto ResidualCorrPanel  ModelDescription ARPolynomial;
ods output ResidualCorrPanel = res_LogChina;
run;
title "Fit Model for Log Income of China -- GARCH";
data res_LogChina;
set res_LogChina(keep = residual);
res_sq = residual**2;
proc arima data = res_LogChina;
identify var = res_sq nlag = 12 stationarity = (adf=4);
ods select ChiSqAuto SeriesCorrPanel;
run;
/*Model : AR(5)*/

title "Log Net Income of China";
proc arima data = Flight_adj;
identify var = net_logchina nlag = 15 stationarity = (adf=4) ;
run;
/*Stationary*/

/*a瑾 EVA*/
title "Fit Model for Log Income of EVA";
proc arima data = Flight_adj;
identify var = logeva(1) nlag = 15 stationarity = (adf=4);
estimate p = 5 method = ml;
ods select ChiSqAuto StationarityTests SeriesCorrPanel ChiSqAuto ResidualCorrPanel  ModelDescription ARPolynomial;
ods output ResidualCorrPanel = res_LogEva;
run;
data res_LogEva;
set res_LogEva(keep = residual);
res_sq = residual**2;
proc arima data = res_LogEva;
identify var = res_sq nlag = 12 stationarity = (adf=4);
ods select ChiSqAuto SeriesCorrPanel;
run;
/*Model : AR(5)*/

title "Log Net Income of EVA";
proc arima data = Flight_adj;
identify var = net_logeva nlag = 15 stationarity = (adf=4) ;
estimate p = 5 method = ml;
ods output ResidualCorrPanel = res_netLogEva;
ods select ChiSqAuto StationarityTests SeriesCorrPanel ChiSqAuto ResidualCorrPanel  ModelDescription ARPolynomial;
run;
data res_netLogEva;
set res_netLogEva(keep = residual);
res_sq = residual**2;
proc arima data = res_netLogEva;
identify var = res_sq nlag = 12 stationarity = (adf=4);
ods select ChiSqAuto SeriesCorrPanel;
run;
/*Model : AR(5)*/

ods pdf close;
